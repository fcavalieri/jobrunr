plugins {
    id 'java-platform'
    id 'maven-publish'
    id 'signing'
}

version = System.getenv('DRONE_TAG') ?: "v1.0.0-SNAPSHOT"

dependencies {
    constraints {
        api 'org.slf4j:slf4j-api:1.7.30'
        api 'org.ow2.asm:asm:9.1'
        api 'org.ow2.asm:asm-util:9.1'

        api 'org.slf4j:slf4j-simple:1.7.30'
        api 'com.fasterxml.jackson.core:jackson-databind:2.12.3'
        api 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.12.3'
        api 'com.google.code.gson:gson:2.8.6'
        api 'org.eclipse:yasson:1.0.8' //todo upgrade
        api 'io.insert-koin:koin-core:2.2.2'

        api 'org.xerial:sqlite-jdbc:3.34.0'
        api 'com.h2database:h2:1.4.200'
        api 'org.postgresql:postgresql:42.2.20'
        api 'com.google.cloud.sql:postgres-socket-factory:1.1.0'
        api 'com.oracle.database.jdbc:ojdbc8:21.1.0.0'
        api 'org.mariadb.jdbc:mariadb-java-client:2.7.2'
        api 'mysql:mysql-connector-java:8.0.24'
        api 'com.microsoft.sqlserver:mssql-jdbc:9.2.1.jre8'
        api 'com.ibm.db2.jcc:db2jcc:db2jcc4'
        api 'com.zaxxer:HikariCP:4.0.3'
        api 'org.apache.commons:commons-dbcp2:2.8.0'
        api 'com.mchange:c3p0:0.9.5.5'
        api 'org.apache.tomcat:tomcat-jdbc:10.0.5'
        api 'redis.clients:jedis:3.6.0'
        api 'io.lettuce:lettuce-core:6.1.0.RELEASE'
        api 'org.mongodb:mongodb-driver-sync:4.2.0'
        api 'org.elasticsearch.client:elasticsearch-rest-high-level-client:7.11.2'
        api 'com.marklogic:marklogic-client-api:5.3.0'

        api 'org.junit.jupiter:junit-jupiter:5.7.0'
        api 'org.awaitility:awaitility:4.0.3'
        api 'org.assertj:assertj-core:3.19.0'
        api 'com.tngtech.archunit:archunit-junit5:0.18.0'
        api 'net.javacrumbs.json-unit:json-unit-assertj:2.25.0'
        api 'org.mockito:mockito-core:3.9.0'
        api 'org.mockito:mockito-junit-jupiter:3.9.0'
        api 'ch.qos.logback:logback-classic:1.2.3'
        api 'org.testcontainers:testcontainers:1.15.3'
        api 'org.testcontainers:junit-jupiter:1.15.3'
        api 'org.testcontainers:jdbc:1.15.3'
        api 'org.testcontainers:mariadb:1.15.3'
        api 'org.testcontainers:mssqlserver:1.15.3'
        api 'org.testcontainers:mysql:1.15.3'
        api 'org.testcontainers:oracle-xe:1.15.3'
        api 'org.testcontainers:postgresql:1.15.3'
        api 'org.testcontainers:db2:1.15.3'
        api 'org.testcontainers:elasticsearch:1.15.3'
    }
}

nexusPublishing {
    packageGroup = "org.jobrunr.jobrunr-bom"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'org.jobrunr'
            artifactId = 'jobrunr-bom'
            version = project.version.replace("v", "")
            from components.javaPlatform

            pom {
                name = 'JobRunr BOM'
                description = 'An easy way to perform background processing on the JVM. Backed by persistent storage. Open and free for commercial use.'
                url = 'https://github.com/jobrunr/jobrunr'
                licenses {
                    license {
                        name = 'Multi licensed'
                        url = 'https://github.com/jobrunr/jobrunr/blob/master/License.md'
                    }
                }
                developers {
                    developer {
                        id = 'rdehuyss'
                        name = 'Ronald Dehuysser'
                        email = 'ronald.dehuysser@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/jobrunr/jobrunr.git'
                    developerConnection = 'scm:git@github.com:jobrunr/jobrunr.git'
                    url = 'https://github.com/jobrunr/jobrunr.git'
                }
            }
        }
    }
}

signing {
    String base64Key = System.getenv('SIGNING_KEY')
    if (base64Key) {
        useInMemoryPgpKeys(new String(Base64.decoder.decode(base64Key)), System.getenv('SIGNING_PASSWORD'))
        sign publishing.publications.mavenJava
    }
}