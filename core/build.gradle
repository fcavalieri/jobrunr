import java.text.SimpleDateFormat

plugins {
    id 'java-library'
    id 'java-test-fixtures'
    id 'maven-publish'
}

sourceSets {
    main {
        resources {
            exclude '**/node_modules/*'
        }
    }
}

task myJavadocs(type: Javadoc) {
    source = sourceSets.main.allJava
    classpath = project.sourceSets.main.compileClasspath
    options.links 'https://docs.oracle.com/en/java/javase/11/docs/api/'
}

dependencies {
    api platform(project(':platform'))

    api 'org.slf4j:slf4j-api'
    api 'org.ow2.asm:asm'
    api 'org.bouncycastle:bcpkix-jdk15on:1.68'

    compileOnly 'com.fasterxml.jackson.core:jackson-databind'
    compileOnly 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    compileOnly 'com.google.code.gson:gson'
    compileOnly 'org.eclipse:yasson'
    compileOnly 'io.micrometer:micrometer-core'

    compileOnly 'com.oracle.database.jdbc:ojdbc8'
    compileOnly 'redis.clients:jedis'
    compileOnly 'io.lettuce:lettuce-core'
    compileOnly 'org.mongodb:mongodb-driver-sync'
    compileOnly 'org.elasticsearch.client:elasticsearch-rest-high-level-client'
    compileOnly 'com.marklogic:marklogic-client-api'

    testImplementation 'org.ow2.asm:asm-util'
    testImplementation 'com.fasterxml.jackson.core:jackson-databind'
    testImplementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    testImplementation 'com.google.code.gson:gson'
    testImplementation 'org.eclipse:yasson'
    testImplementation 'io.micrometer:micrometer-core'
    testImplementation 'org.testcontainers:testcontainers'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:mariadb'
    testImplementation 'org.testcontainers:mssqlserver'
    testImplementation 'org.testcontainers:mysql'
    testImplementation 'org.testcontainers:oracle-xe'
    testImplementation 'org.testcontainers:postgresql'
    testImplementation 'org.testcontainers:db2'
    testImplementation 'org.testcontainers:elasticsearch'
    testImplementation 'org.xerial:sqlite-jdbc'
    testImplementation 'com.h2database:h2'
    testImplementation 'org.postgresql:postgresql'
    testImplementation 'com.google.cloud.sql:postgres-socket-factory'
    testImplementation 'com.microsoft.sqlserver:mssql-jdbc'
    testImplementation 'com.ibm.db2:jcc'
    testImplementation 'com.oracle.database.jdbc:ojdbc8'
    testImplementation 'org.mariadb.jdbc:mariadb-java-client'
    testImplementation 'mysql:mysql-connector-java'
    testImplementation 'com.zaxxer:HikariCP'
    testImplementation 'org.apache.commons:commons-dbcp2'
    testImplementation 'com.mchange:c3p0'
    testImplementation 'org.apache.tomcat:tomcat-jdbc'
    testImplementation 'redis.clients:jedis'
    testImplementation 'io.lettuce:lettuce-core'
    testImplementation 'org.mongodb:mongodb-driver-sync'
    testImplementation 'org.elasticsearch.client:elasticsearch-rest-high-level-client'
    testImplementation 'com.marklogic:marklogic-client-api'

    testFixturesApi 'org.assertj:assertj-core'
    testFixturesApi 'org.junit.jupiter:junit-jupiter'
    testFixturesApi 'net.javacrumbs.json-unit:json-unit-assertj'
    testFixturesApi 'ch.qos.logback:logback-classic'
    testFixturesApi 'com.marklogic:marklogic-client-api'
    testFixturesApi 'org.testcontainers:testcontainers'
    testFixturesImplementation 'org.awaitility:awaitility'
}

java {
    withJavadocJar()
    withSourcesJar()
}

compileTestFixturesJava {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

jar {
    manifest {
        attributes 'Built-By': 'build.jobrunr.io'
        attributes 'Build-Timestamp': new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date())
        attributes 'Build-Version': project.version.replace("v", "")
        attributes 'Bundle-Version': project.version.replace("v", "")
        attributes 'Created-By': "Gradle ${gradle.gradleVersion}"
        attributes 'Build-Jdk': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})"
        attributes 'Build-OS': "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'org.jobrunr'
            artifactId = 'jobrunr'
            version = project.version.replace("v", "")
            from components.java

            pom {
                name = 'JobRunr'
                description = 'An easy way to perform background processing on the JVM. Backed by persistent storage. Open and free for commercial use.'
                url = 'https://github.com/fcavalieri/jobrunr'
                licenses {
                    license {
                        name = 'GNU Lesser General Public License v3.0 or later'
                        url = 'https://github.com/fcavalieri/jobrunr/blob/master/License.md#lgpl-v3-license'
                    }
                    license {
                        name = 'Commercial License'
                        url = 'https://github.com/fcavalieri/jobrunr/blob/master/License.md#commercial-license'
                    }
                }
                developers {
                    developer {
                        id = 'rdehuyss'
                        name = 'Ronald Dehuysser'
                        email = 'ronald.dehuysser@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/fcavalieri/jobrunr.git'
                    developerConnection = 'scm:git@github.com:fcavalieri/jobrunr.git'
                    url = 'https://github.com/fcavalieri/jobrunr.git'
                }

                versionMapping {
                    usage('java-runtime'){
                        fromResolutionResult()
                    }
                }

                withXml {
                    def pomNode = asNode()
                    pomNode.remove(pomNode.dependencyManagement)
                    pomNode.dependencies.'*'.findAll() {
                        it.optional.text() == 'true'
                    }.each() {
                        it.parent().remove(it)
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url "https://gitlab.com/api/v4/projects/26366632/packages/maven"
            credentials(HttpHeaderCredentials) {
                name = System.getenv("PRIVATE_TOKEN") != null ? "Private-Token" : "Job-Token"
                value = System.getenv("PRIVATE_TOKEN") != null ? System.getenv("PRIVATE_TOKEN") : System.getenv("CI_JOB_TOKEN")
            }
            authentication {
                header(HttpHeaderAuthentication)
            }
        }
    }
}
